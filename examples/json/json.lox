// Based on https://www.json.org/json-en.html
// DISCLAIMER: This is provided for illustrative purposes only. Absolutely no
// effort was made to verify full compliance with the JSON specification.

@lexer

OCURLY   = '{'
CCURLY   = '}'
OBRACKET = '['
CBRACKET = ']'
COMMA    = ','
COLON    = ':'

TRUE  = 'true'
FALSE = 'false'
NULL  = 'null'

@macro HEX = [0-9a-fA-F]

@frag '"' @push_mode(String)
@mode String {
  STRING = '"'  @pop_mode
  @frag [\u0020-\U0010FFFF]-[\\]
  @frag '\\' ["\\/bfnrt]
  @frag '\\u' HEX HEX HEX HEX
}

@macro ONE_NINE = [1-9]
@macro DIGIT    = '0' | ONE_NINE
@macro INTEGER  = DIGIT
                | ONE_NINE DIGIT+
                | '-' DIGIT
                | '-' ONE_NINE DIGIT+
@macro FRACTION = '.' [0-9]+
@macro EXPONENT = [eE] [+-]? [1-9] [0-9]*
NUMBER = INTEGER FRACTION? EXPONENT?

@frag [ \r\n\t]+  @discard

@parser

@start json = value

value = object
      | array
      | STRING
      | NUMBER
      | 'true'
      | 'false'
      | 'null'


object = '{' @list(member, ',')? '}'

member = STRING ':' value

array = '[' @list(value, ',')? ']'
