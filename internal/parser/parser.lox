//
//     __         ______     __  __
//    /\ \       /\  __ \   /\_\_\_\
//    \ \ \____  \ \ \/\ \  \/_/\_\/_
//     \ \_____\  \ \_____\   /\_\/\_\
//      \/_____/   \/_____/   \/_/\/_/
//
//

@token
  ID           'identifier'
  LITERAL      'string'
  NUM          'number'

  ZERO_OR_MANY '*'
  ONE_OR_MANY  '+'
  ZERO_OR_ONE  '?'

  COMMA        ','
  CPAREN       ')'
  DEFINE       '='
  OPAREN       '('
  OR           '|'
  SEMICOLON    ';'

  ERROR_KEYWORD '@error'
  LEFT          '@left'
  LIST          '@list'
  RIGHT         '@right'
  TOKEN         '@token' ;

parser    = decl* ;
decl      = rule
          | token_decl
          | @error ';' ;

rule      = ID '=' @list(prod, '|') ';' ;
prod      = term_card+ qualif? ;
term_card = term card? ;
term      = ID | LITERAL | list | '@error' ;
list      = '@list' '(' term ',' term ')' ;
card      = '*' | '+' | '?' ;
qualif    = '@left' '(' NUM ')'
          | '@right' '(' NUM ')' ;

token_decl = TOKEN token+ ';' ;
token      = ID LITERAL? ;
